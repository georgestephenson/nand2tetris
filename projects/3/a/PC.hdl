// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    Inc16(in=rout, out=routPlus1);
    Mux16(a=routPlus1, b=in, sel=load, out=routPlus1OrIn);
    Mux16(a=routPlus1OrIn, b[0..15]=false, sel=reset, out=routPlus1OrInOrReset);
    Or(a=reset, b=load, out=selResetOrLoad);
    Or(a=selResetOrLoad, b=inc, out=selResetOrLoadOrInc);
    Register(in=routPlus1OrInOrReset, load=selResetOrLoadOrInc, out=rout, out=out);
}
